pkgname=ikbus-tools-git
_pkgname=ik-bus-userbase
pkgver=0.3.c13f36b
pkgrel=1
pkgdesc="I/K-bus user space applications"
arch=('armv6h' 'armv7h')
url="https://github.com/korolv/ik-bus-userbase"
license=('LGPL3')
depends=('linux-ikbus-raspberrypi' 'glibc')
makedepends=("git")
provides=("ikbus-tools")
conflicts=("ikbus-tools")
source=("git+https://github.com/korolv/ik-bus-userbase.git")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "0.%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_pkgname}/tools"
    make
}

package() {
    cd "${srcdir}/${_pkgname}/tools"
    install -d "$pkgdir"/usr/bin
    make PREFIX="$pkgdir/usr" install
}
